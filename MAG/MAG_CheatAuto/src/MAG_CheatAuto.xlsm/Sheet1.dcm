VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Call SetRange
    
    If Not Intersect(Target, 검색어) Is Nothing Then
    
        키목록.Interior.Color = vbWhite
        
        If IsEmpty(Target.Value) Then
            Target.Value = " "
        End If
        
        If 검색어.Offset(0, 1) = "기본" Then
            키목록_시작.Formula2 = "=FILTER(StringItem!A:B,ISNUMBER((FIND(""" & 검색어.Value & """,StringItem!B:B))),"" 검색 결과가 없습니다."")"
            
        Else
            키목록_시작.Formula2 = "=FILTER(StringProduct!A:B,ISNUMBER((FIND(""" & 검색어.Value & """,StringProduct!B:B))),"" 검색 결과가 없습니다."")"
        End If

        Target.Select
        
    End If
    
End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    
    Call SetRange
    
    If IsError(키목록_시작) Then
        Exit Sub
    End If
    
    If Not Intersect(Target, 키목록_시작.Offset(-1, 0)) Is Nothing Or Not Intersect(Target, Range("A:A")) Is Nothing Or Not Intersect(Target, Range("C:C")) Is Nothing Or Not Intersect(Target, 키목록_끝.Offset(1, 0)) Is Nothing Then
        
        Exit Sub
        
    '선택한 셀이 열 리스트 안이라면 동작
    ElseIf Not Intersect(Target, 키목록) Is Nothing Then
    
        '셀 색상 확인 및 색 적용
        If Target.Offset(0, -1).Interior.Color = vbRed Then
            Target.Offset(0, -1).Interior.Color = vbWhite
        Else
            Target.Offset(0, -1).Interior.Color = vbRed
        End If
        
    End If
    
    '치트키 영역 선택 시 복사
    If Not Intersect(Target, Range(치트키, 치트키_시작.Offset(-1, 0))) Is Nothing Then
        
        '셀 2개 이상 선택 시 종료
        If Target.Cells.Count > 1 Then
            Exit Sub
        
        '조회된 TID~~ 선택 시 종료
        ElseIf IsEmpty(Target) = True Or InStr(Target.Value, "조회된") > 0 Or InStr(Target.Value, "일괄 입력") > 0 Then
            Exit Sub
        End If
    
        Target.Copy
        
    End If
End Sub
